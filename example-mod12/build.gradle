
buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "forge"
			url = "https://maven.minecraftforge.net/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:${forgeGradle3}"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
	}
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
	maven {
		name = "forge"
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		name = "lukflug"
		url = "http://localhost:8080/maven/"
	}
}

configurations {
	shadow
	implementation.extendsFrom shadow	
}

minecraft {
	mappings("stable",project.mappings12)
	runs {
		client {
			workingDirectory project.file("run")
			property "forge.logging.markers","SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level","debug"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft12}"
	shadow "com.lukflug:panelstudio:${version}-SNAPSHOT"
	shadow "com.lukflug:panelstudio-mc12:${version}-SNAPSHOT"
}

processResources {
	inputs.property "version",project.version
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version":project.version,"mcversion":"1.12.2"
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

shadowJar {
	classifier = ""
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.examplemod12.com.lukflug.panelstudio"
}

reobf {
	shadowJar{}
}
