
buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:${loomVersion}"
		classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
	}
}

apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

configurations {
	shadow
	modImplementation.extendsFrom shadow
}

repositories {
	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}
}


loom {
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft17}"
	mappings "net.fabricmc:yarn:${mappings17}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader17}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${api17}"
	shadow "com.lukflug:panelstudio:${version}"
	shadow "com.lukflug:panelstudio-mc17:${versionmc}"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	classifier = "dev"
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_1"
}

remapJar.dependsOn(shadowJar)
