
buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		flatDir {
			dirs 'libs'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:${loomVersion}"
		classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
	}
}

apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

configurations {
	shadow
	modImplementation.extendsFrom shadow
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}
}


loom {
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft20}"
	mappings "net.fabricmc:yarn:${mappings20}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader20}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${api20}"
	shadow "com.lukflug:panelstudio:${version}"
	shadow "com.lukflug:panelstudio-mc20:${versionmc}"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	archiveClassifier = "dev"
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_4"
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}
